plugins {
    id 'java-library'
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.restaurant'
version = '0.0.1-SNAPSHOT'
description = 'Microservices Architecture'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

ext {
    /**
     * commons-lang3
     */
    commonsLang3Version = "3.19.0"

    /**
     * log version
     */
    slf4jVersion = "2.1.0-alpha1"

    /**
     * lombok version
     */
    lombokVersion = '1.18.42'

    /**
     * Jackson
     */
    jacksonVersion = '2.20.0'
    jacksonDataformatVersion = "2.20.0"
    jacksonAnnotationsVersion = "3.0-rc5"

    /**
     * spring version
     */
    springDependencyManagementVersion = '1.1.7'
    springBootVersion = '3.5.7'
    springCloudVersion = "2025.0.0"
    springFrameworkVersion = '6.2.11'
    spring_security_core_version = '7.0.0'

    /**
     * JWT version
     */
    jjwtVersion = '0.12.3'

    /**
     * Servlet
     */
    servlet_api_version = '6.1.0'

    /**
     * jakarta
     */
    jakarta_persistent_api = '3.2.0'

    /**
     * Bucket Rate
     */
    bucket_rate_version = '4.10.0'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    tasks.register("wrapper", Wrapper) {
        gradleVersion = "8.10.1"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }

    // Exclude unwanted dependencies
    configurations {
        configureEach { config ->
            exclude group: 'commons-logging', module: 'commons-logging'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    // Exclude config files from jar packaging
//    tasks.withType(ProcessResources).configureEach {
//        exclude '/*.yaml'
//        exclude '/*.yml'
//        exclude '/config/*.yml'
//        exclude '/config/*.yaml'
//    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        }
    }

    dependencies {
        /**
         * Logging
         */
        api "org.slf4j:slf4j-api:$slf4jVersion"

        /**
         * Lombok
         */
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"

        /**
         * Jackson
         */
        api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonDataformatVersion"
        api "com.fasterxml.jackson.core:jackson-annotations:$jacksonAnnotationsVersion"
        api "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        api "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"

        /**
         * Apache commons
         */
        api "org.apache.commons:commons-lang3:$commonsLang3Version"

        /**
         * Testing
         */
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}

/**
 * Service project categories
 */
ext {
    allServiceProjects = [
        'auth-service',
        'api-gateway',
        'menu-service',
        'order-service',
        'profile-service',
        'reservation-service',
        'eureka-server',
        'config-service'
    ]

    businessServices = [
        'auth-service',
        'api-gateway',
        'menu-service',
        'order-service',
        'profile-service',
        'reservation-service'
    ]

    webMvcServices = [
        'auth-service',
        'menu-service',
        'order-service',
        'profile-service',
        'reservation-service'
    ]
}

/**
 * Configure all subprojects based on their category
 */
subprojects { subproject ->
    // Apply Spring Boot plugin to service projects (must be done during configuration phase)
    if (subproject.name in rootProject.ext.allServiceProjects) {
        apply plugin: 'org.springframework.boot'
    }

    // Use pluginManager to add dependencies after plugins are applied
    pluginManager.withPlugin('org.springframework.boot') {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
        }

        // Eureka Server
        if (subproject.name == 'eureka-server') {
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-web'
                implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
            }
        }

        // Config Server
        if (subproject.name == 'config-service') {
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter'
                implementation 'org.springframework.cloud:spring-cloud-config-server'
            }
        }

        // Business microservices (config client, eureka client, JWT)
        if (subproject.name in rootProject.ext.businessServices) {
            dependencies {
                // Spring Cloud Config & Discovery
                implementation 'org.springframework.cloud:spring-cloud-starter-config'
                implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

                // JWT
                implementation "io.jsonwebtoken:jjwt-api:${rootProject.ext.jjwtVersion}"
                runtimeOnly "io.jsonwebtoken:jjwt-impl:${rootProject.ext.jjwtVersion}"
                runtimeOnly "io.jsonwebtoken:jjwt-jackson:${rootProject.ext.jjwtVersion}"
            }
        }

        // Web MVC services (excludes api-gateway which uses WebFlux)
        if (subproject.name in rootProject.ext.webMvcServices) {
            dependencies {
                // Spring Boot Web MVC Stack
                implementation 'org.springframework.boot:spring-boot-starter-web'
                implementation 'org.springframework.boot:spring-boot-starter-security'
                implementation 'org.springframework.boot:spring-boot-starter-validation'
                implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

                // Database - PostgreSQL
                runtimeOnly 'org.postgresql:postgresql'

                // Testing
                testImplementation 'org.springframework.security:spring-security-test'
            }
        }
    }
}
